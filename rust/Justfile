help:
	@just --list

build:
	@cargo build --release

run-year YEAR:
	@cargo run --release --bin y{{YEAR}}

run YEAR DAY:
	@RUST_BACKTRACE=1 cargo run --release --bin y{{YEAR}} {{DAY}}

progress:
	@cargo run --quiet --release --bin y2015 -- progress
	@cargo run --quiet --release --bin y2016 -- progress
	@cargo run --quiet --release --bin y2017 -- progress
	@cargo run --quiet --release --bin y2018 -- progress
	@cargo run --quiet --release --bin y2019 -- progress
	@cargo run --quiet --release --bin y2020 -- progress
	@cargo run --quiet --release --bin y2021 -- progress
	@cargo run --quiet --release --bin y2022 -- progress
	@cargo run --quiet --release --bin y2023 -- progress
	@cargo run --quiet --release --bin y2024 -- progress

profile YEAR DAY:
	@mkdir -p ./target/flamegraphs
	@cargo flamegraph --flamechart --deterministic --root --open --output ./target/flamegraphs/{{YEAR}}-{{DAY}}-$(date +%s).svg --bin y{{YEAR}} {{DAY}}

get-input YEAR DAY:
	@test -s .session || { echo "Advent of Code session file not found (.session)."; exit 1; }
	mkdir -p ../inputs/{{YEAR}}
	curl --cookie "session=$(cat .session)" "https://adventofcode.com/{{YEAR}}/day/{{trim_start_match(DAY, '0')}}/input" -o ../inputs/{{YEAR}}/{{DAY}}.in 

new YEAR DAY:
	just get-input {{YEAR}} {{DAY}} \
	&& cp --no-clobber template.rs ./src/y{{YEAR}}/src/d{{DAY}}.rs \
	&& sed -i 's/DAY/{{DAY}}/g' ./src/y{{YEAR}}/src/d{{DAY}}.rs \
	&& sed -i 's/\/\/ @mod@/mod d{{DAY}};\n\/\/ @mod@/g' ./src/y{{YEAR}}/src/main.rs \
	&& sed -i 's/type D{{DAY}} = Unsolved/type D{{DAY}} = d{{DAY}}::Day{{DAY}}/g' ./src/y{{YEAR}}/src/main.rs \
